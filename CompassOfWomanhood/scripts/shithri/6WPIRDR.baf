//
// Attack any enemies
//
IF
  !Allegiance(Myself,ENEMY)
  See([ENEMY])
  !Class(LastSeenBy(Myself),INNOCENT)
  !Class(LastSeenBy(Myself),FLAMING_FIST)
THEN
  RESPONSE #100
    EquipMostDamagingMelee()
    AttackReevaluate([ENEMY],30)
END


// It would be very awkward if the seaman talks between Ployer cursing Jaheira
// and Jaheira commenting on it.
//
// If Jaheira and Ployer are present, wait for Jaheira to get cursed.
IF
  InParty("Jaheira")
  InMyArea("Jaheira")
  InMyArea("Ployer")
  GlobalLT("JaheiraCursed","GLOBAL",3)
  Global("6W#ShithriDrinksSeamanWait","GLOBAL",0)
THEN
  RESPONSE #100
    SetGlobal("6W#ShithriDrinksSeamanWait","GLOBAL",1)
END

// ...then wait a bit more.
IF
  Global("6W#ShithriDrinksSeamanWait","GLOBAL",1)
  GlobalGT("JaheiraCursed","GLOBAL",2)
THEN
  RESPONSE #100
    SetGlobal("6W#ShithriDrinksSeamanWait","GLOBAL",2)
    // wait just a bit after Jaheira stops talking, not to break the moment
    RealSetGlobalTimer("6W#ShithriDrinksSeamanTimer","GLOBAL",20)
END


//
// Shithri's drink quest seaman
//
// Appears in AR0313 (Sea Bounty tavern) after the party collects
// at least different 9 drinks (and re-enters it, if they got to 9
// in the Sea Bounty tavern itself).

// Doesn't really make much sense for the dialogue to take place
// unless Player1 is in the same location. Shithri being there without
// Player1 could trigger a different one, but it seems a bit of an edgecase.
IF
  See("6WSHITHRI")
  IsValidForPartyDialogue("6WSHITHRI")
  InMyArea(Player1)
  InMyArea("THUMB")
  Global("6W#ShithriDrinksSeamanSaw","GLOBAL",0)

  // Wait for Jaheira
  OR(2)
    Global("6W#ShithriDrinksSeamanWait","GLOBAL",0)
    RealGlobalTimerExpired("6W#ShithriDrinksSeamanTimer","GLOBAL")
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    SetGlobal("6W#ShithriDrinksSeamanSaw","GLOBAL",1)
    StartDialogNoSet(Player1)
    SetInterrupt(TRUE)
END
